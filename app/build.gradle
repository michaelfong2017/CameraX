apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.michael.camerax"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:$rootProject.ext.protobuf_version:osx-x86_64"
        }
        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$rootProject.ext.grpc_version:osx-x86_64" // CURRENT_GRPC_VERSION
            }
            // Adding gRPC Kotlin to your project
            grpckt {
                artifact = "io.grpc:protoc-gen-grpc-kotlin:$rootProject.ext.grpc_kotlin_version:jdk7@jar"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                }
                task.plugins {
                    grpc { // Options added to --grpc_out
                        option 'lite'
                    }
                    // Adding gRPC Kotlin to your project
                    grpckt { // Options added to --grpc_out
                        option 'lite'
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.java_version
        targetCompatibility rootProject.ext.java_version
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.java_version
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def camerax_version = "1.0.0"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha24"

    /** grpc */
    // You need to depend on the lite runtime library, not protobuf-java
    implementation "com.google.protobuf:protobuf-javalite:$rootProject.ext.protobuf_version"

    implementation "io.grpc:grpc-okhttp:$rootProject.ext.grpc_version" // CURRENT_GRPC_VERSION
    implementation "io.grpc:grpc-protobuf-lite:$rootProject.ext.grpc_version" // CURRENT_GRPC_VERSION
    implementation "io.grpc:grpc-stub:$rootProject.ext.grpc_version" // CURRENT_GRPC_VERSION
    // Adding gRPC Kotlin to your project
    implementation "io.grpc:grpc-kotlin-stub:$rootProject.ext.grpc_kotlin_version"

}